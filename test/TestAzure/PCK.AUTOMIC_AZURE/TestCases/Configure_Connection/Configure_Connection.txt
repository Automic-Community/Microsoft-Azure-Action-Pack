*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Default Tags
Test Template     Configure Connection Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        CertificatePath    CertificatePassword
Successfull Azure Connection
                      [Tags]    main_scenarios
                      OK        ${Subscription_ID}    ${Cert_Path}       ${Cert_Password}

Azure Connection with Invalid Subscription ID
                      NOK       123-12-112-12-12      ${Cert_Path}       ${Cert_Password}

Azure Connection with Invalid Certificate Location
                      NOK       ${Subscription_ID}    C:\\Cert.jks       ${Cert_Password}

Azure Connection with Corrupted Certificate
                      NOK       ${Subscription_ID}    ${Invalid_Cert}    ${Cert_Password}

Azure Connection with Invalid Certificate Password
                      NOK       ${Subscription_ID}    ${Cert_Path}       abcd

Assert Subscription ID as a mandatory field for Azure Connection
                      OTHER     ${EMPTY}              ${Cert_Path}       ${Cert_Password}

Assert Certificate Path as a mandatory field for Azure Connection
                      OTHER     ${Subscription_ID}    ${EMPTY}           ${Cert_Password}

Assert Certificate Password as a mandatory field for Azure Connection
                      OTHER     ${Subscription_ID}    ${Cert_Path}       ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "Configure Connection"*******
    Log    *******Connect to AE*******
    Connect AE

Configure Connection Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Certificate_Path}    ${Certificate_Password}
    [Documentation]    This teste validates connection between AE and Azure with a precondition for successful scenario that certificate corresponding to jks file should be uploaded to Azure portal.
    ...    - ${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Certificate_Path}: This field specifies the path to the Azure certificate(.jks file).
    ...    -${Certificate_Password}: This field specifies the password for Azure certificate.
    Action Create    ${_CONFIGURE_CONNECTION_ACTION}
    Configure Connection    ${Subscription_ID}    ${Certificate_Path}    ${Certificate_Password}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Report Should Contain    ${AZURE_SUBSCRIPTION_INFO}

Teardown
    Log    *******End Test "Configure Connection"*******
