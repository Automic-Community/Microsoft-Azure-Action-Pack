*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        configure_connection
Default Tags
Test Template     Configure Connection Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        KeystorePath        KeystorePassword
Successfull Azure Connection
                      [Tags]    main_scenarios
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}

Azure Connection with Invalid Subscription ID
                      NOK       123-12-112-12-12      ${Path_Keystore}    ${Password_Keystore}

Azure Connection with Invalid Keystore Location
                      NOK       ${Subscription_ID}    C:\\Cert.jks        ${Password_Keystore}

Azure Connection with Corrupted Keystore
                      NOK       ${Subscription_ID}    ${Invalid_Cert}     ${Password_Keystore}

Azure Connection with Invalid Keystore Password
                      NOK       ${Subscription_ID}    ${Path_Keystore}    abcd

Assert Subscription ID as a mandatory field
                      OTHER     ${EMPTY}              ${Path_Keystore}    ${Password_Keystore}

Assert Keystore Path as a mandatory field
                      OTHER     ${Subscription_ID}    ${EMPTY}            ${Password_Keystore}

Assert Keystore Password as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "CONFIGURE CONNECTION"*******
    Log    *******Connect to AE*******
    Connect AE

Configure Connection Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    [Documentation]    This test validates connection between AE and Azure with a precondition for successful scenario that certificate corresponding to jks file should be uploaded to Azure portal.
    ...    - ${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Keystore_Path}: This field specifies the path to the Keystore(.jks file).
    ...    -${Keystore_Password}: This field specifies the password for Keystore.
    Action Create    ${_CONFIGURE_CONNECTION_ACTION}
    Configure Connection    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Report Should Contain    ${AZURE_SUBSCRIPTION_INFO}

Teardown
    Log    *******End Test "CONFIGURE CONNECTION"*******
