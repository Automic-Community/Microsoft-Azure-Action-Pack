*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        check_request_status
Default Tags
Test Template     Check Request Status Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        KeystorePath        KeystorePassword        RequestID
Asserting Valid Request ID
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ValidID

Asserting Invalid Request ID
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    b2ekof439dksksi9

Asserting Request ID as Mandatory Field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "CHECK REQUEST STATUS"*******
    Log    *******Connect to AE*******
    Connect AE

Check Request Status Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}    ${Request_ID}
    [Documentation]    This test retrieves the status of the azure request id.
    ...    - ${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Keystore_Path}: This field specifies the path to the Keystore(.jks file).
    ...    -${Keystore_Password}: This field specifies the password for Keystore.
    ...    -${Request_ID}: This field specifies Request ID generated by azure providing status of an command to be executed.
    Run Keyword If    '${OK_NOK}' == 'OK'    Change VM State    ${_CHANGE_VM_STATE_ACTION}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    ...    ${VM_Name}    ${Cloud_Service_Name}    ${Deployment_Name}    SHUTDOWN_DEALLOCATE_VM
    Action Create    ${_CHECK_REQUEST_STATUS_ACION}
    Configure Connection    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    Run Keyword If    '${Request_ID}' == 'ValidID'    Action Set    &UC4RB_AZR_REQUEST_ID#    ${Generated_ID}
    Run Keyword If    '${Request_ID}' != 'ValidID'    Action Set    &UC4RB_AZR_REQUEST_ID#    ${Request_ID}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AZR_REQUEST_STATUS#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AZR_REQUEST_STATUS#

Teardown
    Log    *******End Test "CHECK REQUEST STATUS"*******
