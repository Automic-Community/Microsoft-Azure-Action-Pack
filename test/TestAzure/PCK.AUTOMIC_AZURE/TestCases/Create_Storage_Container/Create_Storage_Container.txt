*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_storage_container
Default Tags
Test Template     Create Storage Container Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    AccountName                AccessKey                ContainerName    AccessType
Asserting Creation of Blob with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Name_Account}            ${Key_Access}            cactest1         BLOB

Asserting Creation of Container with valid inputs
                      OK        ${Name_Account}            ${Key_Access}            cactest1         CONTAINER

Asserting Creation of Private Container with valid inputs
                      OK        ${Name_Account}            ${Key_Access}            cactest1         PRIVATE

Asserting Creation of Blob with Invalid Account Name
                      NOK       ${Invalid_Name_Account}    ${Key_Access}            cactest1         BLOB

Asserting Creation of Blob with Invalid Access Key
                      NOK       ${Name_Account}            ${Invalid_Key_Access}    cactest1         BLOB

Asserting Creation of Blob with Invalid Container Name
                      NOK       ${Name_Account}            ${Key_Access}            ABC_1            BLOB

Assert Account Name as a mandatory field
                      OTHER     ${EMPTY}                   ${Key_Access}            cactest1         BLOB

Assert Access Key as a mandatory field
                      OTHER     ${Name_Account}            ${EMPTY}                 cactest1         BLOB

Assert Container Name as a mandatory field
                      OTHER     ${Name_Account}            ${Key_Access}            ${EMPTY}         BLOB

*** Keywords ***
Startup
    Log    *******Start Test "CREATE STORAGE CONTAINER"*******
    Log    *******Connect to AE*******
    Connect AE

Create Storage Container Template
    [Arguments]    ${OK_NOK}    ${Account_Name}    ${Access_Key}    ${Container_Name}    ${Access_Type}
    [Documentation]    This test validates storage container creation in provided account name.
    ...    -Preconditions: To run this test valid Account name(cactest) should exist on Azure portal and its access key should be used also it should not contain any container with name cactest1.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Account_Name}: This field specifies name of the storage account in whcih storage conatiner is to br created.
    ...    -${Access_Key}: This field specifies Access Key for the provided storage account.
    ...    -${Container_Name}: This field specifies name of the container to be created.
    ...    -${Access_Type}: This field specifies the type of access to be provided that is blob or container or private container.
    Run Keyword If    '${OK_NOK}' == 'OK'    Sleep    30s
    Action Create    ${_CREATE_STORAGE_ACTION}
    Storage Account Info    ${Account_Name}    ${Access_Key}
    Action Set    &UC4RB_AZR_CONTAINER_NAME#    ${Container_Name}
    Action Set    &UC4RB_AZR_CONTAINER_ACCESS#    ${Access_Type}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Delete Storage Container    ${_DELETE_STORAGE_CONTAINER_ACTION}    ${Name_Account}    ${Key_Access}    cactest1
    ...    ${EMPTY}

Teardown
    Log    *******End Test "CREATE STORAGE CONTAINER"*******
