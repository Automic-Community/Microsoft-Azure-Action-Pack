*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        change_vm_state
Default Tags
Test Template     Change VM State Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        KeystorePath        KeystorePassword        NameVM                NameCloudService                 NameDeployment                Command
Starting a VM         OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Deployment_Name}            START_VM

Restarting a VM       [Tags]    main_scenarios
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Deployment_Name}            RESTART_VM

Stopping a VM         OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Deployment_Name}            SHUTDOWN_VM

Stopping an Already Stopped VM
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Deployment_Name}            SHUTDOWN_VM

Stopping and Deallocating a VM
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Deployment_Name}            SHUTDOWN_DEALLOCATE_VM

Assertion with Invalid VM Name
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Invalid_VM_Name}    ${Cloud_Service_Name}            ${Deployment_Name}            RESTART_VM

Assertion with Invalid Cloud Service Name
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Invalid_Cloud_Service_Name}    ${Deployment_Name}            START_VM

Assertion with Invalid Deployement Name
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Invalid_Deployment_Name}    START_VM

Assert VM Name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Empty}              ${Cloud_Service_Name}            ${Deployment_Name}            START_VM

Assert Cloud Service Name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Empty}                         ${Deployment_Name}            START_VM

Assert Deployment Name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Empty}                      START_VM

Assert Command Field as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Deployment_Name}            ${Empty}

*** Keywords ***
Startup
    Log    *******Start Test "CHANGE VM STATE"*******
    Log    *******Connect to AE*******
    Connect AE

Change VM State Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}    ${Name_VM}    ${Name_Cloud_Service}
    ...    ${Name_Deployment}    ${Command}
    [Documentation]    This test asserts change in state of the VM's.
    ...    - ${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Keystore_Path}: This field specifies the path to the Keystore(.jks file).
    ...    -${Keystore_Password}: This field specifies the password for Keystore.
    ...    -${Name_VM}: This field specifies the name of the VM on which command is to be executed.
    ...    -${Name_Cloud_Service}: This field specifies the name of the Cloud Service in which VM is located.
    ...    -${Name_Deployment}: This field specifies the name of the Deployment in which VM is located.
    ...    -${Command}: This field specifies the command which is to be executed on VM's.
    Action Create    ${_CHANGE_VM_STATE_ACTION}
    Configure Connection    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    Action Set    &UC4RB_AZR_VM_NAME#    ${Name_VM}
    Action Set    &UC4RB_AZR_CLOUD_SERVICE#    ${Name_Cloud_Service}
    Action Set    &UC4RB_AZR_DEPLOYMENT#    ${Name_Deployment}
    Action Set    &UC4RB_AZR_COMMAND#    ${Command}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AZR_REQUEST_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AZR_REQUEST_ID#

Teardown
    Log    *******End Test "CHANGE VM STATE"*******
