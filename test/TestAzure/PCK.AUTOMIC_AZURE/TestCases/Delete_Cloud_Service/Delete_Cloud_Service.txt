*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_cloud_service
Default Tags
Test Template     Delete Cloud Service Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        KeystorePath        KeystorePassword        NameCloudService                 DeleteAttachedMedia
Delete Cloud Service without any deployments
                      [Tags]    main_scenarios
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    blank                            NO

Delete Cloud Service with Non-existing Cloud Service Name
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Invalid_Cloud_Service_Name}    NO

Assert cloud service name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${EMPTY}                         NO

*** Keywords ***
Startup
    Log    *******Start Test "DELETE CLOUD SERVICE"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Cloud Service Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}    ${Name_Cloud_Service}    ${Delete_Attached_Media}
    [Documentation]    This test validates removal of cloud service on azure portal.
    ...    -Precondition: Name of cloud service to remove should be same as used in create cloud service parameter file.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Keystore_Path}: This field specifies the path to the Keystore(.jks file).
    ...    -${Keystore_Password}: This field specifies the password for Keystore.
    ...    -${Name_Cloud_Service}: This field specifies the Cloud Service name which is to be deleted.
    ...    -${Delete_Attached_Media}: This field specifies whether to delete attached medias to the cloud service. eg: Deployments
    Run Keyword If    '${Name_Cloud_Service}' == 'blank'    Create Cloud Service    ${_CREATE_CLOUD_SERVICE_ACTION}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    ...    ${Cloud_Service_File}
    Action Create    ${_DELETE_CLOUD_SERVICE_ACTION}
    Configure Connection    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    Run Keyword If    '${Name_Cloud_Service}' == 'blank'    Action Set    &UC4RB_AZR_CLOUD_SERVICE#    test-ankit23
    Run Keyword If    '${Name_Cloud_Service}' != 'blank'    Action Set    &UC4RB_AZR_CLOUD_SERVICE#    ${Name_Cloud_Service}
    Action Set    &UC4RB_AZR_DELETE_MEDIA#    ${Delete_Attached_Media}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AZR_REQUEST_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AZR_REQUEST_ID#

Teardown
    Log    *******End Test "DELETE CLOUD SERVICE"*******
