*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_deployment
Default Tags
Test Template     Create Deployment Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        KeystorePath        KeystorePassword        NameCloudService                 DeploymentSlot    ParameterFile
Create Deployment in Production Slot
                      [Tags]    main_scenarios
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    blank                            Production        ${Create_Deployment_FilePath}

Create Deployment in Staging Slot
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    blank                            Staging           ${Create_Deployment_FilePath}

Create Deployment with non-existing Cloud Service
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Invalid_Cloud_Service_Name}    Production        ${Create_Deployment_FilePath}

Create Deployment with invalid path to Parameter File
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Cloud_Service_Name}            Production        c://abc.xml

Create Deployment with invalid Parameter File
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Cloud_Service_Name}            Production        ${Invalid_Create_Deployment_FilePath}

Assert cloud service name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${EMPTY}                         Production        ${Create_Deployment_FilePath}

Assert Parameter File as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Cloud_Service_Name}            Production        ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "CREATE DEPLOYMENT"*******
    Log    *******Connect to AE*******
    Connect AE

Create Deployment Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}    ${Name_Cloud_Service}    ${Deployment_Slot}
    ...    ${Parameter_File}
    [Documentation]    This test validates creation of Deployment on azure portal.
    ...    -Precondition: For this action a container "test" under "cactest" storage account name should contain "HelloMartians.cspkg" file which is under EXECDIR of Robot Automation Test.
    ...    -Precondition 2: To run this test, parameter file should contain unique cloud service name i.e. non-existing on azure portal.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Keystore_Path}: This field specifies the path to the Keystore(.jks file).
    ...    -${Keystore_Password}: This field specifies the password for Keystore.
    ...    -${Name_Cloud_Service}: This field specifies the Cloud Service name in which deployment is to be created.
    ...    -${Deployment_Slot}: This filed specifies the slot in which deployment is to be created.
    ...    -${Parameter_File}: This field specifies path to the Deployment Parameter file.
    Run Keyword If    '${Name_Cloud_Service}' == 'blank'    Create Cloud Service    ${_CREATE_CLOUD_SERVICE_ACTION}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    ...    ${Cloud_Service_File}
    Action Create    ${_CREATE_DEPLOYMENT_ACTION}
    Configure Connection    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    Run Keyword If    '${Name_Cloud_Service}' == 'blank'    Action Set    &UC4RB_AZR_CLOUD_SERVICE#    test-ankit23
    Run Keyword If    '${Name_Cloud_Service}' != 'blank'    Action Set    &UC4RB_AZR_CLOUD_SERVICE#    ${Name_Cloud_Service}
    Action Set    &UC4RB_AZR_DEPLOYMENT_SLOT#    ${Deployment_Slot}
    Action Set    &UC4RB_AZR_PARAM_FILE#    ${Parameter_File}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AZR_REQUEST_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AZR_REQUEST_ID#
    Run Keyword If    '${Name_Cloud_Service}' == 'blank'    Sleep    120s
    Run Keyword If    '${Name_Cloud_Service}' == 'blank'    Delete Cloud Service    ${_DELETE_CLOUD_SERVICE_ACTION}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    ...    test-ankit23    YES

Teardown
    Log    *******End Test "CREATE DEPLOYMENT"*******
