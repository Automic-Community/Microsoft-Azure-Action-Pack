*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_vm
Default Tags
Test Template     Delete Virtual Machine Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        KeystorePath        KeystorePassword        NameVM                NameCloudService                 NameDeployment                DeleteAttachedMedia
Removing a VM Without Deleting Attached Media
                      [Tags]    main_scenarios
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name_Del}        ${Cloud_Service_Name}            ${Deployment_Name}            NO

Removing a VM also Deleting Attached Media
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name_Del2}       ${Cloud_Service_Name}            ${Deployment_Name}            YES

Assertion with Invalid VM Name
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Invalid_VM_Name}    ${Cloud_Service_Name}            ${Deployment_Name}            NO

Assertion with Invalid Cloud Service Name
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Invalid_Cloud_Service_Name}    ${Deployment_Name}            NO

Assertion with Invalid Deployement Name
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Invalid_Deployment_Name}    NO

Assert VM Name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${EMPTY}              ${Cloud_Service_Name}            ${Deployment_Name}            NO

Assert Cloud Service Name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Empty}                         ${Deployment_Name}            NO

Assert Deployment Name as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VM_Name}            ${Cloud_Service_Name}            ${Empty}                      NO

*** Keywords ***
Startup
    Log    *******Start Test "DELETE VIRTUAL MACHINE"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Virtual Machine Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}    ${Name_VM}    ${Name_Cloud_Service}
    ...    ${Name_Deployment}    ${Delete_Attached_Media}
    [Documentation]    This test asserts removal of VM from Azure portal.
    ...    -Precondition: To run this test, two VM's should be present under CAC-TEST cloud service with name VM1 and VM2(VM2 should contain media disk)
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Keystore_Path}: This field specifies the path to the Keystore(.jks file).
    ...    -${Keystore_Password}: This field specifies the password for Keystore.
    ...    -${Name_VM}: This field specifies the name of the VM on which command is to be executed.
    ...    -${Name_Cloud_Service}: This field specifies the name of the Cloud Service in which VM is located.
    ...    -${Name_Deployment}: This field specifies the name of the Deployment in which VM is located.
    ...    -${Delete_Attached_Media}: This field specifies flag which is used to
    Action Create    ${_DELETE_VM_ACTION}
    Configure Connection    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    Action Set    &UC4RB_AZR_VM_NAME#    ${Name_VM}
    Action Set    &UC4RB_AZR_CLOUD_SERVICE#    ${Name_Cloud_Service}
    Action Set    &UC4RB_AZR_DEPLOYMENT#    ${Name_Deployment}
    Action Set    &UC4RB_AZR_VM_MEDIA#    ${Delete_Attached_Media}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AZR_REQUEST_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AZR_REQUEST_ID#

Teardown
    Log    *******End Test "DELETE VIRTUAL MACHINE"*******
