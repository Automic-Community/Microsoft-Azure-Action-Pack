*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        put_blob
Default Tags
Test Template     Put Blob Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    AccountName        AccessKey        ContainerName    BlobName    FileToUpload        ContentType
Put Blob with valid inputs with file less than 64 mb
                      [Tags]    main_scenarios
                      OK        ${Name_Account}    ${Key_Access}    blank            test        ${Put_Blob_File}    ${EMPTY}

Put Blob with an invalid container name
                      NOK       ${Name_Account}    ${Key_Access}    ABC=1            ${EMPTY}    ${Put_Blob_File}    ${EMPTY}

Put Blob with an invalid path to file which is to be uploaded
                      NOK       ${Name_Account}    ${Key_Access}    test2015         ${EMPTY}    C://abc.xml         ${EMPTY}

Put Blob with an non-existing container name
                      NOK       ${Name_Account}    ${Key_Access}    test2015         ${EMPTY}    ${Put_Blob_File}    ${EMPTY}

Put Blob with an invalid blob name
                      NOK       ${Name_Account}    ${Key_Access}    blank            @@@#        ${Put_Blob_File}    ${EMPTY}

Put Blob with an non-existing account name
                      NOK       testabc            ${Key_Access}    test2015         ${EMPTY}    ${Put_Blob_File}    ${EMPTY}

Put Blob with an invalid access key
                      NOK       ${Name_Account}    1232424312       test2015         ${EMPTY}    ${Put_Blob_File}    ${EMPTY}

Assert blob name and content type as optional field
                      OK        ${Name_Account}    ${Key_Access}    blank            ${EMPTY}    ${Put_Blob_File}    ${EMPTY}

Assert Container Name as a mandatory field
                      OTHER     ${Name_Account}    ${Key_Access}    ${EMPTY}         xyz         ${Put_Blob_File}    zip

Assert Account Name as a mandatory field
                      OTHER     ${EMPTY}           ${Key_Access}    test2015         xyz         ${Put_Blob_File}    zip

Assert Access Key as a mandatory field
                      OTHER     ${Name_Account}    ${EMPTY}         test2015         xyz         ${Put_Blob_File}    zip

Assert File to Upload as a mandatory field
                      OTHER     ${Name_Account}    ${Key_Access}    test2015         xyz         ${EMPTY}            zip

*** Keywords ***
Startup
    Log    *******Start Test "PUT BLOB"*******
    Log    *******Connect to AE*******
    Connect AE

Put Blob Template
    [Arguments]    ${OK_NOK}    ${Account_Name}    ${Access_Key}    ${Container_Name}    ${Blob_Name}    ${File_To_Upload}
    ...    ${Content_Type}
    [Documentation]    This test validates uploading a blob to an existing container.
    ...    - ${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Account_Name}: This field specifies name of the storage account in whcih storage conatiner is to br created.
    ...    -${Access_Key}: This field specifies Access Key for the provided storage account.
    ...    -${Container_Name}: This field specifies name of the container to be deleted.
    ...    -${Blob_Name}: This field specifies name for the blob to be created.
    ...    -${File_To_Upload}: This field specifies path to a file that should be uploaded.
    ...    -${Content_Type}: This field specifies content type of file.
    Run Keyword If    '${OK_NOK}' == 'OK'    Create Storage Container    ${_CREATE_STORAGE_ACTION}    ${Name_Account}    ${Key_Access}    test123
    ...    BLOB
    Action Create    ${_PUT_BLOB_ACTION}
    Storage Account Info    ${Account_Name}    ${Access_Key}
    Run Keyword If    '${Container_Name}' == 'blank'    Action Set    &UC4RB_AZR_CONTAINER_NAME#    ${Generated_Container_Name}
    Run Keyword If    '${Container_Name}' != 'blank'    Action Set    &UC4RB_AZR_CONTAINER_NAME#    ${Container_Name}
    Action Set    &UC4RB_AZR_BLOB_NAME#    ${Blob_Name}
    Action Set    &UC4RB_AZR_BLOB_FILE#    ${File_To_Upload}
    Action Set    &UC4RB_AZR_BLOB_CONTENT_TYPE#    ${Content_Type}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${Container_Name}' == 'blank'    Delete Storage Container    ${_DELETE_STORAGE_CONTAINER_ACTION}    ${Account_Name}    ${Access_Key}    ${Generated_Container_Name}
    ...    ${EMPTY}

Teardown
    Log    *******End Test "PUT BLOB"*******
