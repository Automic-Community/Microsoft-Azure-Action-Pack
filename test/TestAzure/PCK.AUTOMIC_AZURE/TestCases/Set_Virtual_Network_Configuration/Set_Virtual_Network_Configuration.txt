*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        set_virtual_network_configuration
Default Tags
Test Template     Set Virtual Network Configuration Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SubscriptionID        KeystorePath        KeystorePassword        ConfigurationFile
Set Virtual Network with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${VN_FilePath}

Set Virtual Network with invalid path to Configuration File
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    c://abc.xml

Set Virtual Network with invalid Configuration File
                      NOK       ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${Invalid_VN_FilePath}

Assert Configuration File as a mandatory field
                      OTHER     ${Subscription_ID}    ${Path_Keystore}    ${Password_Keystore}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "SET VIRTUAL NETWORK CONFIGURATION"*******
    Log    *******Connect to AE*******
    Connect AE

Set Virtual Network Configuration Template
    [Arguments]    ${OK_NOK}    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}    ${Configuration_File}
    [Documentation]    This test validates setting up a virtual network on azure portal.
    ...    - ${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Subscription_ID}: This field specifies Subscription ID for Azure.
    ...    -${Keystore_Path}: This field specifies the path to the Keystore(.jks file).
    ...    -${Keystore_Password}: This field specifies the password for Keystore.
    ...    -${Configuration_File}: This field specifies path to the virtual network configuration file.
    Action Create    ${_SET_VIRTUAL_NETWORK_ACTION}
    Configure Connection    ${Subscription_ID}    ${Keystore_Path}    ${Keystore_Password}
    Action Set    &UC4RB_AZR_VN_CONFIG_FILE#    ${Configuration_File}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AZR_REQUEST_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AZR_REQUEST_ID#

Teardown
    Log    *******End Test "SET VIRTUAL NETWORK CONFIGURATION"*******
